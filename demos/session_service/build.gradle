// Copyright 2023 The Android Open Source Project
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.
plugins {
    id "com.google.protobuf" version "0.9.5"
}
apply from: "$gradle.ext.androidxMediaSettingsDir/common_library_config.gradle"
apply plugin: 'kotlin-android'

android {
    namespace 'androidx.media3.demo.session.service'

    compileSdk project.ext.compileSdkVersion

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    defaultConfig {
        versionName project.ext.releaseVersion
        versionCode project.ext.releaseVersionCode
        minSdkVersion project.ext.minSdkVersion
        targetSdkVersion project.ext.appTargetSdkVersion
    }

    buildTypes {
        release {
            signingConfig signingConfigs.debug
        }
        debug {
            jniDebuggable = true
        }
    }

    lintOptions {
        // The demo service module isn't indexed, and doesn't have translations.
        disable 'GoogleAppIndexingWarning','MissingTranslation'
    }
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:4.31.1'
    }
    generateProtoTasks {
        all().forEach { task ->
            task.plugins {
                create("java")
            }
        }
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:' + androidxCoreVersion
    implementation 'androidx.datastore:datastore:1.1.5'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-guava:1.9.0'
    implementation 'com.google.protobuf:protobuf-java:4.31.1'
    implementation project(modulePrefix + 'lib-cast')
    implementation project(modulePrefix + 'lib-common-ktx')
    implementation project(modulePrefix + 'lib-exoplayer')
    implementation project(modulePrefix + 'lib-exoplayer-dash')
    implementation project(modulePrefix + 'lib-exoplayer-hls')
    implementation project(modulePrefix + 'lib-ui')
    implementation project(modulePrefix + 'lib-session')
}
