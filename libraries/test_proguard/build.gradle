// Copyright 2025 The Android Open Source Project
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.
apply from: '../../constants.gradle'
apply plugin: 'com.android.application'

android {
    namespace 'androidx.media3.test.proguard'

    sourceSets {
        androidTest.assets.srcDir '../test_data/src/test/assets/'
        test.assets.srcDir '../test_data/src/test/assets/'
    }

    compileSdk project.ext.compileSdkVersion

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        versionName project.ext.releaseVersion
        versionCode project.ext.releaseVersionCode
        minSdkVersion project.ext.minSdkVersion
        targetSdkVersion project.ext.appTargetSdkVersion
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    buildTypes {
        // Run R8 for all build types to discover potential proguard problems.
        configureEach {
            shrinkResources true
            minifyEnabled true
            proguardFiles = [
                'proguard-rules.txt',
                getDefaultProguardFile('proguard-android.txt')
            ]
            testProguardFile 'proguard-test-rules.txt'
        }
    }
}

dependencies {
    implementation project(modulePrefix + 'lib-exoplayer')
    implementation project(modulePrefix + 'lib-exoplayer-dash')
    implementation project(modulePrefix + 'lib-exoplayer-hls')
    implementation project(modulePrefix + 'lib-exoplayer-rtsp')
    implementation project(modulePrefix + 'lib-exoplayer-smoothstreaming')
    implementation project(modulePrefix + 'lib-transformer')
    implementation project(modulePrefix + 'lib-ui')
    implementation project(modulePrefix + 'lib-datasource-rtmp')
    implementation project(modulePrefix + 'lib-decoder-vp9')
    implementation project(modulePrefix + 'lib-decoder-opus')
    implementation project(modulePrefix + 'lib-decoder-flac')
    implementation project(modulePrefix + 'lib-decoder-ffmpeg')
    implementation project(modulePrefix + 'lib-decoder-midi')
    implementation project(modulePrefix + 'lib-decoder-av1')
    implementation project(modulePrefix + 'lib-decoder-iamf')
    implementation 'androidx.appcompat:appcompat:' + androidxAppCompatVersion
    implementation 'com.google.guava:guava:' + guavaVersion
    compileOnly 'org.jetbrains.kotlin:kotlin-annotations-jvm:' + kotlinAnnotationsVersion
    androidTestImplementation 'androidx.test:core:' + androidxTestCoreVersion
    androidTestImplementation 'androidx.test.ext:junit:' + androidxTestJUnitVersion
    androidTestImplementation 'androidx.test:runner:' + androidxTestRunnerVersion
}
