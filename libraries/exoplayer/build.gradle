// Copyright (C) 2016 The Android Open Source Project
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.
apply from: "$gradle.ext.androidxMediaSettingsDir/common_library_config.gradle"

android {
    namespace 'androidx.media3.exoplayer'

    defaultConfig {
        // The following argument makes the Android Test Orchestrator run its
        // "pm clear" command after each test invocation. This command ensures
        // that the app's state is completely cleared between tests.
        testInstrumentationRunnerArguments clearPackageData: 'true'
    }

    buildTypes {
        debug {
            testCoverageEnabled = true
        }
    }

    sourceSets {
        androidTest.assets.srcDir '../test_data/src/test/assets'
        test.assets.srcDir '../test_data/src/test/assets/'
    }

    publishing {
        singleVariant('release') {
            withSourcesJar()
        }
    }
}

dependencies {
    api project(modulePrefix + 'lib-common')
    api project(modulePrefix + 'lib-container')
    // TODO(b/203754886): Revisit which modules are exported as API dependencies.
    api project(modulePrefix + 'lib-datasource')
    api project(modulePrefix + 'lib-decoder')
    api project(modulePrefix + 'lib-extractor')
    api project(modulePrefix + 'lib-database')
    implementation 'androidx.annotation:annotation:' + androidxAnnotationVersion
    implementation 'androidx.collection:collection:' + androidxCollectionVersion
    implementation 'androidx.core:core:' + androidxCoreVersion
    implementation 'androidx.exifinterface:exifinterface:1.3.6'
    compileOnly 'com.google.code.findbugs:jsr305:' + jsr305Version
    compileOnly 'org.checkerframework:checker-qual:' + checkerframeworkVersion
    compileOnly 'org.jetbrains.kotlin:kotlin-annotations-jvm:' + kotlinAnnotationsVersion
    compileOnly 'com.google.errorprone:error_prone_annotations:' + errorProneVersion
    androidTestImplementation 'androidx.test:runner:' + androidxTestRunnerVersion
    androidTestImplementation 'com.linkedin.dexmaker:dexmaker:' + dexmakerVersion
    androidTestImplementation 'com.linkedin.dexmaker:dexmaker-mockito:' + dexmakerVersion
    androidTestImplementation(project(modulePrefix + 'test-utils')) {
        exclude module: modulePrefix.substring(1) + 'library-core'
    }
    androidTestImplementation 'com.squareup.okhttp3:mockwebserver:' + okhttpVersion
    testImplementation 'com.squareup.okhttp3:mockwebserver:' + okhttpVersion
    testImplementation 'org.robolectric:robolectric:' + robolectricVersion
    testImplementation project(modulePrefix + 'test-utils')
    testImplementation project(modulePrefix + 'test-utils-robolectric')
}

ext {
    releaseArtifactId = 'media3-exoplayer'
    releaseName = 'Media3 ExoPlayer module'
}
apply from: '../../publish.gradle'
